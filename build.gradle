
buildscript {
    ext {
        kotlinVersion = '1.5.10'
        wpiLibVersion = "2022.1.1-beta-3"
        junit5Version = "5.8.2" // https://junit.org/junit5/
        gdxVersion = '1.9.13' // https://github.com/libgdx/libgdx/releases
        abstractDashboardVersion = "v1.1.0"
        abstractControllerLibVersion = "v2.5.0"
        jacksonVersion = "2.13.0" // https://github.com/FasterXML/jackson-databind/tags
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}
plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
}

allprojects {

    version = '1.0'

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
//        maven { url "http://first.wpi.edu/FRC/roborio/maven/release" }
        maven { url "https://frcmaven.wpi.edu/artifactory/release" }
        maven { url "https://jitpack.io" }
    }

}
subprojects {
    apply plugin: "kotlin"
    apply plugin: "maven-publish"
    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
    }
    test {
        useJUnitPlatform()
    }
    compileKotlin {
        kotlinOptions {
            // WPILib requires Java 11, so lets use its features!
            jvmTarget = "11"
        }
    }
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    task packageSources(type: Jar) {
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }
    artifacts {
        archives packageSources
    }
    publishing {
        publications {
            mavenJava(MavenPublication) {
                // thanks https://stackoverflow.com/a/68515859/5434860
                groupId = "${group}"
                artifactId = "${project.name}"
                version = "${version}"

                from project.components.java
            }
        }
    }
}
project(":api"){
    dependencies {
//        // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
//        api group: 'org.slf4j', name: 'slf4j-api', version: '1.7.28'
    }
}

project(":gdx") {
    apply plugin: "kotlin"
    dependencies {
        api project(":api")
    }
}
project(":desktop") {
    dependencies {
        api project(":gdx")
    }
}
project(":wpi"){
    dependencies {
        api project(":api")
    }
}

wrapper {
    gradleVersion = '7.3.2' // https://gradle.org/releases/
    distributionType = Wrapper.DistributionType.ALL
}
