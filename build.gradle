
buildscript {
    ext {
        kotlinVersion = '1.4.21'
        wpiLibVersion = "2021.1.1-beta-4" // TODO change
        junit5Version = "5.7.0" // https://junit.org/junit5/
        gdxVersion = '1.9.12' // https://github.com/libgdx/libgdx/releases
        abstractDashboardVersion = "v1.1.0"
        abstractControllerLibVersion = "v2.5.0"
        jacksonVersion = "2.12.0"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}
plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
}

allprojects {

    version = '1.0'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
//        maven { url "http://first.wpi.edu/FRC/roborio/maven/release" }
        maven { url "https://frcmaven.wpi.edu/artifactory/release" }
        maven { url "https://jitpack.io" }
    }

}
subprojects {
    apply plugin: "kotlin"
    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
    }
    test {
        useJUnitPlatform()
    }
    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    task packageSources(type: Jar) {
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }
    artifacts {
        archives packageSources
    }

}
project(":api"){
    dependencies {
//        // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
//        api group: 'org.slf4j', name: 'slf4j-api', version: '1.7.28'
    }
}

project(":gdx") {
    apply plugin: "kotlin"
    dependencies {
        api project(":api")
    }
}
project(":desktop") {
    dependencies {
        api project(":gdx")
    }
}
project(":wpi"){
    dependencies {
        api project(":api")
    }
}

wrapper {
    gradleVersion = '6.7.1' // https://gradle.org/releases/
    distributionType = Wrapper.DistributionType.ALL
}
